apply plugin: 'war'

group = 'nl.eveoh.mytimetable'
version = '2.1.4'

sourceCompatibility = 1.6
targetCompatibility = 1.6

ext.jacksonVersion = '2.2.3'
ext.blackboardVersion = '9.1.120113.0'
ext.logbackVersion = '1.0.13'
ext.slf4jVersion = '1.7.5'
ext.guavaVersion = '16.0.1'

war {
    // Replace version in Blackboard Manifest
    inputs.property('version', version)

    filesMatching("**/bb-manifest.xml") {
        expand version: version
    }

    manifest {
        attributes(
                'Implementation-Title': 'MyTimetable Blackboard building block',
                'Implementation-Version': version,
                'Implementation-Vendor': 'Eveoh',
                'Provider': 'Gradle'
        )
    }

    manifest.from(jar.manifest)
}

configurations {
    // Exclude commons-logging dependency, the SLF4J bridge will take care of this
    all*.exclude group: 'commons-logging', module: 'commons-logging'
}

repositories {
    maven {
        url "https://maven.eveoh.nl/content/groups/public"
    }
}

dependencies {
    providedCompile 'javax.servlet:servlet-api:2.5'
    providedCompile 'javax.servlet:jsp-api:2.0'

    compile "ch.qos.logback:logback-classic:${logbackVersion}"
    compile "org.slf4j:slf4j-api:${slf4jVersion}"
    compile "org.slf4j:jcl-over-slf4j:${slf4jVersion}"

    compile 'org.apache.commons:commons-lang3:3.1'
    compile "com.google.guava:guava:${guavaVersion}"

    compile 'nl.eveoh.mytimetable:mytimetable-api-client:1.3.1'

    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"

    // Blackboard provided libs
    providedCompile "blackboard.platform:bb-cms-admin:${blackboardVersion}"
    providedCompile "blackboard.platform:bb-platform:${blackboardVersion}"
    providedCompile "blackboard.platform:bb-taglibs:${blackboardVersion}"

    providedCompile 'javax.servlet:jstl:1.2'
}
